   //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_store
    STR_VAR
           store=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%store%~
                          file_prefix=~STO~
                          edits
                          editstring
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_store
    STR_VAR
           store=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%store%~
                          file_prefix=~STO~
                          edits editstring allow_missing
                          tv=EVALUATE_BUFFER ~%tv%~

         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching regexp
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_all_stores
    STR_VAR
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION regexp_template
                   STR_VAR       file_prefix=~STO~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~
         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< sto_offsets.2da
TYPE                 offset_loc      offset_length       entrynum_loc  entrynum_length   entry_length
item_purchased       0x2c            4                   0x30          4                 0x4
item                 0x34            4                   0x38          4                 0x1c
drink                0x4c            4                   0x50          4                 0x14
cure                 0x70            4                   0x74          4                 0xc
>>>>>>>>

OUTER_SPRINT ~offset_readin_lookup_2da~ ~sto_offsets~
OUTER_SPRINT ~offset_readin_file_prefix~ ~STO~
LAUNCH_ACTION_MACRO read_in_offsets


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY sto_item_fields BEGIN
     item_resource => ~0x0,ASCII~
     item_charges_1 => ~0xa,SHORT~
     item_charges_2 => ~0xc,SHORT~
     item_charges_3 => ~0xe,SHORT~
     item_num_in_stock => ~0x14,LONG~
     items_infinite => ~0x18,LONG~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=sto_item_fields offset_base=~offset_base~ function_prefix=STO END

ACTION_DEFINE_ASSOCIATIVE_ARRAY sto_bit_fields BEGIN
      state_identified => ~0x10,0~
      state_unstealable => ~0x10,1~
      state_stolen  => ~0x10,2~
END

LAF build_flag_data_field_editors STR_VAR offset_base=offset_base lookup_table=sto_bit_fields function_prefix=STO END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             Item-removal hook
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION STO_remove_items
   STR_VAR arguments=""
BEGIN
   PATCH_IF ~%arguments%~ STRING_COMPARE_CASE "" BEGIN
        LPF return_first_entry STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET arguments=list item=entry END
        REMOVE_STORE_ITEM ~%item%~
        LPF STO_remove_items STR_VAR arguments END
   END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~STO_remove_items - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              General  adder               syntax: list of item or item(n)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION add_items_to_store
   STR_VAR store=""
           items=""
           location=""
           tv="no"
BEGIN
   ACTION_IF ~%store%~ STRING_COMPARE_CASE ~~ BEGIN
      LAF action_return_first_entry STR_VAR list=EVALUATE_BUFFER ~%store%~ RET this_store=entry store=list END
      ACTION_IF ~%tv%~ STRING_EQUAL_CASE ~yes~ BEGIN
        OUTER_SPRINT ~this_store~ ~%tutu_var%%this_store%~
      END
      COPY_EXISTING ~%this_store%.sto~ ~override~
      SPRINT ~items_here~ ~%items%~
        WHILE ~%items_here%~ STRING_COMPARE_CASE "" BEGIN
           SPRINT item_num 1
           LPF return_first_entry STR_VAR list=EVALUATE_BUFFER ~%items_here%~ RET items_here=list entry=entry END
           LPF return_function_and_argument STR_VAR input=EVALUATE_BUFFER ~%entry%~ RET to_add=function item_data=argument END
           PATCH_IF ~%item_data%~ STRING_COMPARE_CASE ~~ BEGIN
              SPRINT item_num ~%item_data%~
           END
           SPRINT input ~ADD_STORE_ITEM %to_add% %location% #1 #0 #0 "IDENTIFIED" #%item_num%~
           LPF patch_reinclude_this STR_VAR input END
        END
      BUT_ONLY
      LAF add_items_to_store STR_VAR store items location tv END
   END
END
