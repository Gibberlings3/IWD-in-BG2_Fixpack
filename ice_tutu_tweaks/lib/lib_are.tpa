//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_area
    STR_VAR
           area=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%area%~
                          file_prefix=~ARE~
                          edits
                          editstring
                          allow_missing
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_area
    STR_VAR
           area=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%area%~
                          file_prefix=~ARE~
                          edits editstring allow_missing
         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching regexp
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_all_areas
    STR_VAR
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION regexp_template
                   STR_VAR       file_prefix=~ARE~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< area_offsets.2da
TYPE       offset_loc      offset_length       entrynum_loc  entrynum_length   entry_length
actor      0x54            4                   0x58          2                 0x110
trigger    0x5c            4                   0x5a          2                 0xc4
spawn      0x60            4                   0x64          4                 0xc8
entrance   0x68            4                   0x6c          4                 0x68
container  0x70            4                   0x74          2                 0xc0
item       0x78            4                   0x76          2                 0x14
vertices   0x7c            4                   0x80          2                 0x4
ambients   0x84            4                   0x82          2                 0xd4
doors      0xa8            4                   0xa4          4                 0xc8
animations 0xb0            4                   0xac          4                 0x4c
tiles      0xb8            4                   0xb4          4                 0
songs      0xbc            4                   0x0           0                 0
restspawn  0xc0            4                   0x0           0                 0
mapnote    0xc4            4                   0xc8          4                0x34
explored   0xa0            4                   0x9c          4                0
>>>>>>>>
OUTER_SPRINT ~offset_readin_lookup_2da~ ~area_offsets~
OUTER_SPRINT ~offset_readin_file_prefix~ ~ARE~
LAUNCH_ACTION_MACRO read_in_offsets


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits - absolute
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY are_fields_absolute BEGIN
     area_script=>~0x94,ASCII~
     rain_probability=>~0x4a,SHORT~
     snow_probability=>~0x4c,SHORT~
     fog_probability=>~0x4e,SHORT~
     lightning_probability=>~0x50,SHORT~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=are_fields_absolute function_prefix=ARE END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits - relative to offsets
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY are_fields BEGIN
   actorname => ~0x0,ASCII20~
   actor_name => ~0x0,ASCII20~
   script_override=>~0x50,ASCII~
   script_class => ~0x60,ASCII~
   script_race => ~0x68,ASCII~
   script_general => ~0x58,ASCII~
   script_default => ~0x70,ASCII~
   script_specifics => ~0x78,ASCII~
   crefile => ~0x80,ASCII~
   actor_resource => ~0x80,ASCII~
   actor_x_coord_start => ~0x20,SHORT~
   actor_y_coord_start => ~0x22,SHORT~
   actor_x_coord_dest => ~0x24,SHORT~
   actor_y_coord_dest => ~0x26,SHORT~
   actor_orientation => ~0x34,SHORT~
   dialog => ~0x48,ASCII~
   door_name=>~0x0,ASCII20~
   door_icon=>~0x7c,LONG~
   item_resource => ~0x0,ASCII~
   item_charges_1 => ~0xa,SHORT~
   item_charges_2 => ~0xc,SHORT~
   item_charges_3 => ~0xe,SHORT~
   trigger_type =>~0x20,SHORT~
   entrance_name=>~0x0,ASCII20~
   entrance_xloc=>~0x20,SHORT~
   entrance_yloc=>~0x22,SHORT~
   entrance_orientation=>~0x24,SHORT~
   trigger_name=>~0x0,ASCII20~
   trigger_type=>~0x20,SHORT~
   trigger_info=>~0x64,LONG~
   trigger_dest=>~0x38,ASCII~
   spawn_name=>~0x0,ASCII20~
   spawn_xloc=>~0x20,SHORT~
   spawn_yloc=>~0x22,SHORT~
   spawn_creature1=>~0x24,ASCII~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=are_fields offset_base=~offset_base~ function_prefix=ARE END


ACTION_DEFINE_ASSOCIATIVE_ARRAY are_bit_fields BEGIN
      // exits
      trap_undetectable => ~0x60,0~
      reset_trap => ~0x60,1~
      party_required => ~0x60,2~
      trap_detectable => ~0x60,3~
      trap_set_off_by_NPC => ~0x60,6~
      trigger_deactivated => ~0x61,0~
      impassable_by_NPC => ~0x61,1~
END

LAF build_flag_data_field_editors STR_VAR lookup_table=are_bit_fields offset_base=~offset_base~ function_prefix=ARE  END



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               composite data-field edits
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ARE_actor_x_coord
      INT_VAR offset_base=0
      STR_VAR arguments=""
BEGIN
      LAUNCH_PATCH_FUNCTION ARE_actor_x_coord_start  INT_VAR offset_base = offset_base STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
      LAUNCH_PATCH_FUNCTION ARE_actor_x_coord_dest  INT_VAR offset_base = offset_base STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_actor_x_coord - -~ END


DEFINE_PATCH_FUNCTION ARE_actor_y_coord
      INT_VAR offset_base=0
      STR_VAR arguments=""
BEGIN
      LAUNCH_PATCH_FUNCTION ARE_actor_y_coord_start  INT_VAR offset_base = offset_base STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
      LAUNCH_PATCH_FUNCTION ARE_actor_y_coord_dest  INT_VAR offset_base = offset_base STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_actor_y_coord - -~ END

DEFINE_PATCH_FUNCTION ARE_wipe_spawns
     INT_VAR offset_base=0
BEGIN
     WRITE_ASCII 0x24+offset_base ~~ (0x50)
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_wipe_spawns - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               baseline data - for adding actors
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ARE_actor_baseline
         INT_VAR offset_base=0
BEGIN
         WRITE_LONG offset_base + 0x28 9 // CRE loaded=unknown [seems to be default]
         WRITE_LONG offset_base + 0x40 0xffffffff // present at all times
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_actor_baseline - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               looks for a script anywhere and returns 1 if true
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ARE_match_script
     INT_VAR
         offset_base=0
     STR_VAR
         arguments=""
     RET
         value
BEGIN
   SET value=0
   PATCH_FOR_EACH script IN override class race general default specifics BEGIN
      LPF ~ARE_read_script_%script%~ INT_VAR offset_base=offset_base RET scriptvalue=value END
      PATCH_IF ~%scriptvalue%~ STRING_EQUAL_CASE ~%arguments%~ BEGIN
         SET value=1
      END
   END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_match_script - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               swaps a script
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ARE_swap_scripts
     STR_VAR
         arguments=""
BEGIN
   LPF walk_entries
            STR_VAR function=ARE_swap_script_core
                    arguments=EVALUATE_BUFFER ~%arguments%~
                    file_prefix=EVALUATE_BUFFER ~%file_prefix%~
                    entry_type=actor
            END
END

DEFINE_PATCH_FUNCTION ARE_swap_script_core
     INT_VAR
        offset_base=0
     STR_VAR
        arguments=""
BEGIN
   LPF process_pair STR_VAR input=EVALUATE_BUFFER ~%arguments%~ RET oldscript=key newscript=value END
   PATCH_FOR_EACH script IN override class race general default specifics BEGIN
      LPF ~ARE_read_script_%script%~ INT_VAR offset_base=offset_base RET scriptvalue=value END
      PATCH_IF ~%scriptvalue%~ STRING_EQUAL_CASE ~%oldscript%~ BEGIN
           LPF ~ARE_script_%script%~ INT_VAR offset_base=offset_base STR_VAR arguments=EVALUATE_BUFFER ~%newscript%~ END
      END
   END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_swap_scripts - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               gets the area script (and allocates one if needed)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION get_area_script
    STR_VAR area=""
    RET script
BEGIN
   COPY_EXISTING ~%area%.are~ ~override~
       LPF ARE_read_area_script RET script=value END
       PATCH_IF (~%script%~ STRING_EQUAL_CASE ~none~ || ~%script%~ STRING_EQUAL_CASE ~~) BEGIN
          INNER_PATCH_SAVE ~script~ ~%area%~ BEGIN
             READ_ASCII 0x0 fwcheck (2)
             PATCH_IF ~%fwcheck%~ STRING_EQUAL_CASE ~FW~ BEGIN
                INSERT_BYTES 0x0 1
                WRITE_ASCII 0x0 ~_AR~
             END
          END
          LPF ARE_area_script STR_VAR arguments=EVALUATE_BUFFER ~%script%~ END
       END
   BUT_ONLY
   ACTION_IF !FILE_EXISTS_IN_GAME ~%script%.bcs~ BEGIN
                <<<<<<<< %script%.baf
                >>>>>>>>
                COMPILE ~%script%.baf~
   END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               extends the area script
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION extend_area_script
   STR_VAR area=""
           top=""
           bottom=""
           location=""
           inline="no"
           ssl="no"
BEGIN
   ACTION_IF ~%area%~ STRING_COMPARE ~~ BEGIN
      LAF action_return_first_entry STR_VAR list=EVALUATE_BUFFER ~%area%~ RET entry=entry area=list END
      LAF get_area_script STR_VAR area=EVALUATE_BUFFER ~%entry%~ RET file=script END
      LAF extend STR_VAR file top bottom location inline ssl END
      LAF extend_area_script STR_VAR area top bottom location inline ssl END
   END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              swap an item
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ARE_swap_items
          STR_VAR arguments=""
BEGIN
         PATCH_IF ~%arguments%~ STRING_COMPARE_CASE ~~ BEGIN
             LPF return_first_pair STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET old=key new=value arguments=list END
             REPLACE_TEXTUALLY CASE_INSENSITIVE ~%old%~ ~%new%~ (8)
             LPF ARE_swap_items STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
         END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_swap_items - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              set trigger info
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ARE_say_trigger_info
   STR_VAR arguments="" offset_base=""
BEGIN
   SET offset=offset_base+0x64
   LPF say_this_here INT_VAR offset say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ARE_say_trigger_info - -~ END

