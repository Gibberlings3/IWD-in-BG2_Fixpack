//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_creature
    STR_VAR
           creature_old=""
           creature_new=""
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template 
                  STR_VAR file_old=EVALUATE_BUFFER ~%creature_old%~
                          file_new=EVALUATE_BUFFER ~%creature_new%~
                          file_ending=~CRE~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_creature
    STR_VAR
           creature=""
           edits=""
           editstring=""
    BEGIN
        LAUNCH_ACTION_FUNCTION clone_creature STR_VAR creature_old = EVALUATE_BUFFER ~%creature%~ creature_new = EVALUATE_BUFFER ~%creature%~ edits=EVALUATE_BUFFER ~%edits%~ editstring= EVALUATE_BUFFER ~%editstring%~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               direct command
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_direct_command
     STR_VAR arguments=""

     BEGIN
        INNER_ACTION BEGIN
           LAUNCH_ACTION_FUNCTION decompose_CSV 
                   STR_VAR input=EVALUATE_BUFFER ~%arguments%~
                   RET     argument_0=argument_0
                           argument_1=argument_1
                           argument_2=argument_2
                           argument_3=argument_3
                           argument_4=argument_4
                           argument_5=argument_5
                           argument_6=argument_6
                           argument_7=argument_7
                           argument_8=argument_8
                           argument_9=argument_9
           END
        END
        FOR (i=0;i<10;i=i+1) BEGIN
           PATCH_IF $argument(EVALUATE_BUFFER ~%i%~) STRING_COMPARE_CASE ~null~ BEGIN
                SPRINT ~temp~ $argument(EVALUATE_BUFFER ~%i%~)
                LAUNCH_PATCH_FUNCTION patch_reinclude_this STR_VAR input=EVALUATE_BUFFER ~%temp%~ END
           END
        END

END



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY cre_fields BEGIN
   level     =>    ~0x234,BYTE~
   level2    =>    ~0x235,BYTE~
   level3    =>    ~0x236,BYTE~
   str       =>    ~0x238,BYTE~
   str_ex    =>    ~0x239,BYTE~
   int       =>    ~0x23a,BYTE~
   wis       =>    ~0x23b,BYTE~
   dex       =>    ~0x23c,BYTE~
   con       =>    ~0x23d,BYTE~
   cha       =>    ~0x23e,BYTE~
   script_override  =>    ~0x248,ASCII~
   script_class     =>    ~0x250,ASCII~
   script_race      =>    ~0x258,ASCII~
   script_general   =>    ~0x260,ASCII~
   script_default   =>    ~0x268,ASCII~
   save_vs_death    =>    ~0x54,BYTE~
   save_vs_wands    =>    ~0x55,BYTE~
   save_vs_poly     =>    ~0x56,BYTE~
   save_vs_breath   =>    ~0x57,BYTE~
   save_vs_spell    =>    ~0x58,BYTE~
   thac0            =>    ~0x52,BYTE~
   hp_max           =>    ~0x26,SHORT~
   hp_current       =>    ~0x24,SHORT~
   dv               =>    ~0x280,ASCII20~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=cre_fields function_prefix=CRE END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               composite data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// current and max hp

DEFINE_PATCH_FUNCTION CRE_hitpoints STR_VAR arguments="" BEGIN
   LAUNCH_PATCH_FUNCTION CRE_hp_max STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
   LAUNCH_PATCH_FUNCTION CRE_hp_current STR_VAR arguments=EVALUATE_BUFFER ~%arguments%~ END
END

// all saving throws

DEFINE_PATCH_FUNCTION CRE_saves STR_VAR arguments="" BEGIN
   INNER_ACTION BEGIN
           LAUNCH_ACTION_FUNCTION decompose_CSV 
                     STR_VAR input=EVALUATE_BUFFER ~%arguments%~
                     RET save_vs_death=argument_0
                         save_vs_wands=argument_1
                         save_vs_poly=argument_2
                         save_vs_breath=argument_3
                         save_vs_spell=argument_4
           END
   END
   PATCH_FOR_EACH save IN death wands poly breath spell BEGIN
      SPRINT ~string~ ~LAUNCH_PATCH_FUNCTION CRE_save_vs_%save% STR_VAR arguments=EVALUATE_BUFFER "%percentage%save_vs_%save%%percentage%" END~
      LAUNCH_PATCH_FUNCTION patch_reinclude_this STR_VAR input=EVALUATE_BUFFER ~%string%~ END
   END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               data-field edits involving lookup (I think defining these via a table is more trouble than it's worth)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_class
         INT_VAR arguments=""
         STR_VAR filename=""
     BEGIN
         LAUNCH_PATCH_FUNCTION field_write_with_IDS_lookup INT_VAR offset=0x273 STR_VAR IDS_file="class" IDS_entry=EVALUATE_BUFFER ~%arguments%~ write_length=BYTE RET success=success END
         PATCH_IF ~success~=0 BEGIN
              LAUNCH_PATCH_FUNCTION patch_display_warning STR_VAR warning=EVALUATE_BUFFER "Tried to set nonexistent class %arguments% in class %filename%" END
         END
     END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               strip scripts
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_strip_script  /// takes as argument CSV list of scripts
      STR_VAR arguments=""
BEGIN
      INNER_ACTION BEGIN
         LAUNCH_ACTION_FUNCTION ~decompose_CSV~ 
                STR_VAR 
                     ~input~=EVALUATE_BUFFER ~%arguments%~
                RET
                     argument_0 = argument_0
                     argument_1 = argument_1
                     argument_2 = argument_2
                     argument_3 = argument_3
                     argument_4 = argument_4
                     argument_5 = argument_5
                     argument_6 = argument_6
                     argument_7 = argument_7
                     argument_8 = argument_8
                     argument_9 = argument_9
         END
      END

      FOR (i=0;i<10;i=i+1) BEGIN
         SPRINT ~temp_var~ $argument(EVALUATE_BUFFER ~%i%~)
         TO_LOWER ~temp_var~
         PATCH_IF ~%temp_var%~ STRING_COMPARE_CASE ~null~ BEGIN
              SPRINT $check_array(~%temp_var%~) ~null~
         END
      END
      FOR (i=0x248;i<0x270;i=i+8) BEGIN
         READ_ASCII i ~test~
         TO_LOWER ~test~
         PATCH_IF VARIABLE_IS_SET $check_array(EVALUATE_BUFFER ~%test%~) BEGIN
            WRITE_ASCII i ~~ (8)
         END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               rearrange scripts and add a new one
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION CRE_insert_script_below 
     STR_VAR 
         arguments=""
         filename=""

BEGIN
     FOR (i=0x268;i>0x240;i=i - 8) BEGIN
        READ_ASCII i ~test~
        SET blank=0
        PATCH_IF ~%test%~ STRING_EQUAL_CASE ~~ OR ~%test%~ STRING_EQUAL_CASE ~none~ BEGIN
           SET blank=i
           SET i=0
        END
     END
     PATCH_IF blank=0 BEGIN
          LAUNCH_PATCH_FUNCTION patch_display_warning STR_VAR warning=EVALUATE_BUFFER "Failed to find a space to insert script %arguments% in file %filename%" END
     END ELSE BEGIN
           FOR (i=blank;i<0x270;i=i+8) BEGIN
              READ_ASCII i+8 ~move~
              WRITE_ASCIIE i ~%move%~ (8)
           END
           WRITE_ASCIIE 0x268 ~%arguments%~ (8)
     END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               add spells from a list
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION CRE_add_spell_list
     STR_VAR arguments = ""
BEGIN
     SPRINT ~output~ EVALUATE_BUFFER ~PATCH_FOR_EACH "spell" IN %arguments% BEGIN LAUNCH_PATCH_FUNCTION QUICK_ADD_SPELL STR_VAR spellname = EVALUATE_BUFFER "%spell%" END END~
     LAUNCH_PATCH_FUNCTION patch_reinclude_this STR_VAR input=EVALUATE_BUFFER ~%output%~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               remove all spells
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_remove_spells
    STR_VAR arguments = ""
BEGIN
     REMOVE_KNOWN_SPELLS
     REMOVE_MEMORIZED_SPELLS
END
