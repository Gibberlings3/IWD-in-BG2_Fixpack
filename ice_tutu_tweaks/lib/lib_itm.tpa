//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////                                         
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_item
    STR_VAR
           item=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%item%~
                          file_prefix=~ITM~
                          edits
                          editstring
                          allow_missing
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_item
    STR_VAR
           item=""
           edits=""
           editstring=""
           tv="no"
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%item%~
                          file_prefix=~ITM~
                          edits editstring allow_missing
                          tv=EVALUATE_BUFFER ~%tv%~

         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching regexp
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_all_items
    STR_VAR
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION regexp_template
                   STR_VAR       file_prefix=~ITM~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~
                          tv=yes
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching make
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_item
    STR_VAR
           item=""
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION make_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%item%~
                          file_prefix=~ITM~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~
                          build_before= ITM_build
                          build_after = ITM_postbuild
         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< item_offsets.2da
TYPE       offset_loc      offset_length       entrynum_loc  entrynum_length   entry_length
ability          0x64            4                   0x68          2                 0x38
effect_global    0x6a            4                   0x70          2                 0x30
>>>>>>>>
OUTER_SPRINT ~offset_readin_lookup_2da~ ~item_offsets~
OUTER_SPRINT ~offset_readin_file_prefix~ ~ITM~
LAUNCH_ACTION_MACRO read_in_offsets


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTION_DEFINE_ASSOCIATIVE_ARRAY item_fields BEGIN
   name1_string => ~0x8,LONG~
   name2_string => ~0xc,LONG~
   enchantment => ~0x60,LONG~
   category => ~0x1c,SHORT~
   inventory =>~0x22,ASCII2~
   price => ~0x34,LONG~
   max_in_stack =>~0x38,SHORT~
   icon => ~0x3a,ASCII~
   icon_ground =>~0x44,ASCII~
   icon_carried=>~0x58,ASCII~
   lore=> ~0x42,SHORT~
   description1_string => ~0x50,LONG~
   description2_string => ~0x54,LONG~
   num_abils=> ~0x68,SHORT~
   proficiency_code => ~0x31,BYTE~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=item_fields function_prefix=ITM END

ACTION_DEFINE_ASSOCIATIVE_ARRAY item_ability_fields BEGIN
   ability_type => ~0x0,BYTE~
   ability_icon_loc => ~0x2,BYTE~
   ability_icon => ~0x4,ASCII~
   ability_target=>~0xc,BYTE~
   ability_range=>~0xe,SHORT~
   launcher_required=>~0x10,SHORT~
   speed=> ~0x12,SHORT~
   to_hit => ~0x14,SHORT~
   ability_dicesize=>~0x16,SHORT~
   ability_numdice=>~0x18,SHORT~
   damage_bonus=>~0x1a,SHORT~
   damage_type=>~0x1c,SHORT~
   effect_num=>~0x1e,SHORT~
   effect_offset=>~0x20,SHORT~
   number_charges => ~0x22,SHORT~
   when_drained => ~0x24,BYTE~
   strength_bonus=>~0x26,BYTE~
   when_recharges => ~0x27,SHORT~
   projectile => ~0x2a,SHORT~
   overhand=>~0x2c,SHORT~
   backhand=>~0x2e,SHORT~
   thrust=>~0x30,SHORT~
   is_arrow=>~0x32,SHORT~
   is_bolt=>~0x34,SHORT~
   is_missile=>~0x36,SHORT~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR offset_base=~offset_base~ lookup_table=item_ability_fields function_prefix=ITM END


ACTION_DEFINE_ASSOCIATIVE_ARRAY eff_fields BEGIN
   opcode=>~0,SHORT~
   target=>~0x2,BYTE~
   power=>~0x3,BYTE~
   parameter1=>~0x4,LONG~
   parameter2=>~0x8,LONG~
   timing=>~0xc,BYTE~
   dispel=>~0xd,BYTE~
   duration=>~0xe,LONG~
   probability1=>~0x12,BYTE~
   probability2=>~0x13,BYTE~
   resource=>~0x14,ASCII~
   dicenum=> ~0x1c,LONG~
   dicesize=> ~0x20,LONG~
   save_bonus=>~0x28,LONG~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=eff_fields offset_base=~offset_base~ function_prefix=ITM secondary="yes" END

ACTION_DEFINE_ASSOCIATIVE_ARRAY itm_bit_fields BEGIN
      unsellable => ~0x18,0~
      twohanded => ~0x18,1~
      droppable => ~0x18,2~
      displayable => ~0x18,3~
      cursed => ~0x18,4~
      not_copyable => ~0x18,5~
      magical => ~0x18,6~
      bow => ~0x18,7~
      silver => ~0x19,0~
      cold_iron => ~0x19,1~
      stolen => ~0x19,2~
      conversable => ~0x19,3~
      unusable_chaotic => ~0x1e,0~
      unusable_evil => ~0x1e,1~
      unusable_good => ~0x1e,2~
      unusable_GEneutral => ~0x1e,3~
      unusable_lawful => ~0x1e,4~
      unusable_LCneutral => ~0x1e,5~
      unusable_bard => ~0x1e,6~
      unusable_cleric => ~0x1e,7~
      unusable_cleric_mage => ~0x1f,0~
      unusable_cleric_thief  => ~0x1f,1~
      unusable_cleric_ranger => ~0x1f,2~
      unusable_fighter => ~0x1f,3~
      unusable_fighter_druid => ~0x1f,4~
      unusable_fighter_mage => ~0x1f,5~
      unusable_fighter_cleric => ~0x1f,6~
      unusable_fighter_mage_cleric => ~0x1f,7~
      unusable_fighter_mage_thief => ~0x20,0~
      unusable_fighter_thief => ~0x20,1~
      unusable_mage => ~0x20,2~
      unusable_mage_thief => ~0x20,3~
      unusable_paladin => ~0x20,4~
      unusable_ranger => ~0x20,5~
      unusable_thief => ~0x20,6~
      unusable_elf => ~0x20,7~
      unusable_dwarf => ~0x21,0~
      unusable_half_elf => ~0x21,1~
      unusable_halfling => ~0x21,2~
      unusable_human => ~0x21,3~
      unusable_gnome => ~0x21,4~
      unusable_monk => ~0x21,5~
      unusable_druid => ~0x21,6~
      unusable_half_orc => ~0x21,7~
      unusable_cleric_of_talos=>~0x29,0~
      unusable_cleric_of_helm=>~0x29,1~
      unusable_cleric_of_lathander=>~0x29,2~
      unusable_totemic_druid=>~0x29,3~
      unusable_shapeshifter=>~0x29,4~
      unusable_avenger=>~0x29,5~
      unusable_barbarian=>~0x29,6~
      unusable_wild_mage=>~0x29,7~
      unusable_stalker=>~0x2b,0~
      unusable_beastmaster=>~0x2b,1~
      unusable_assassin=>~0x2b,2~
      unusable_bounty_hunter=>~0x2b,3~
      unusable_swashbuckler=>~0x2b,4~
      unusable_blade=>~0x2b,5~
      unusable_jester=>~0x2b,6~
      unusable_skald=>~0x2b,7~
      unusable_diviner=>~0x2d,0~
      unusable_enchanter=>~0x2d,1~
      unusable_illusionist=>~0x2d,2~
      unusable_invoker=>~0x2d,3~
      unusable_necromancer=>~0x2d,4~
      unusable_transmuter=>~0x2d,5~
      unusable_no_kit=>~0x2d,6~
      unusable_archer=>~0x2d,7~
      unusable_berserker=>~0x2f,0~
      unusable_wizard_slayer=>~0x2f,1~
      unusable_kensai=>~0x2f,2~
      unusable_cavalier=>~0x2f,3~
      unusable_inquisitor=>~0x2f,4~
      unusable_undead_hunter=>~0x2f,5~
      unusable_abjurer=>~0x2f,6~
      unusable_conjurer=>~0x2f,7~
END

LAF build_flag_data_field_editors STR_VAR lookup_table=itm_bit_fields function_prefix=ITM END

ACTION_DEFINE_ASSOCIATIVE_ARRAY eff_bit_fields BEGIN
      save_vs_spell => ~0x24,0~
      save_vs_breath => ~0x24,1~
      save_vs_death => ~0x24,2~
      save_vs_poison => ~0x24,2~
      save_vs_wands => ~0x24,3~
      save_vs_petrification => ~0x24,4~
      save_vs_polymorph => ~0x24,4~
      bypass_mirror_image =>~0x27,0~
END

LAF build_flag_data_field_editors STR_VAR lookup_table=eff_bit_fields offset_base=~offset_base~ function_prefix=ITM  secondary="yes" END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               data-field edits involving lookup
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY ~itm_ids_fields~ BEGIN
   proficiency => ~0x31,BYTE,STATS~
END

LAUNCH_ACTION_FUNCTION build_IDS_data_field_editors STR_VAR lookup_table=itm_ids_fields function_prefix=ITM END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               swap opcodes
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ~swap_opcodes_core~
           INT_VAR
                   offset_base=0
           STR_VAR arguments=""
                   file_prefix=""
BEGIN
           LAUNCH_PATCH_FUNCTION ~process_arrow~ STR_VAR input=EVALUATE_BUFFER ~%arguments%~ RET opcode1=key opcode2=value END
           LAUNCH_PATCH_FUNCTION ~%file_prefix%_read_opcode~ INT_VAR offset_base=~offset_base~ RET oldvalue=value END
           PATCH_IF oldvalue=opcode1 BEGIN
             LAUNCH_PATCH_FUNCTION ~%file_prefix%_opcode~INT_VAR offset_base=~offset_base~ STR_VAR arguments=EVALUATE_BUFFER ~%opcode2%~ END
           END
END

DEFINE_PATCH_FUNCTION ~ITM_swap_opcodes~
          STR_VAR arguments=""
                  file_prefix=""
BEGIN
     LAUNCH_PATCH_FUNCTION walk_effect_tree
          INT_VAR ab_length=0x38
          STR_VAR function=swap_opcodes_core
                  arguments=EVALUATE_BUFFER ~%arguments%~
                  file_prefix=EVALUATE_BUFFER ~%file_prefix%~
     END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_swap_opcodes - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               delete opcodes
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ~ITM_delete_opcodes~
             STR_VAR arguments=""
BEGIN
     PATCH_IF ~%arguments%~ STRING_COMPARE ~~ BEGIN
        LPF return_first_entry STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET arguments=list opcode_to_delete=entry END
        PATCH_MATCH ~%opcode_to_delete%~ WITH
        all BEGIN
             LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete=~-1~ END
             LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete=~-1~ END
        END
        equipped BEGIN
             LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete=~-1~ END
        END
        ability BEGIN
             LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete=~-1~ END
        END
        DEFAULT
          LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete END
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete END
          LPF ITM_delete_opcodes STR_VAR arguments END
        END
     END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_delete_opcodes - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               make a clone of a global effect in an item
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION ~ITM_clone_effect_global~
           STR_VAR arguments=""
BEGIN
   INNER_ACTION BEGIN
      FAIL ~The ITM_clone_effect_global function is obsolete~
   END
   WHILE ~%arguments%~ STRING_COMPARE "" BEGIN
    LPF return_first_pair STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET cloned_opcode=key new_opcode=value arguments=list END
    GET_OFFSET_ARRAY eff_array 0x6a 4 0x70 2 0 0 0x30
    PHP_EACH eff_array AS eff_num => eff_off BEGIN
         SET found_match=0
         PATCH_IF SHORT_AT eff_off = cloned_opcode BEGIN
               SET found_match=1
          /*
               READ_SHORT eff_off+2 template0
               READ_ASCII eff_off+0xc template1 (0x8)
               READ_ASCII eff_off+0x24 template2 (0x8) */
               READ_ASCII eff_off template (0x30)
               SET insert_num=eff_num
         END
    END
    PATCH_IF found_match=1 BEGIN
           LPF ADD_ITEM_EQEFFECT
             INT_VAR opcode=999
                     insert_point=insert_num + 1
           END
           GET_OFFSET_ARRAY eff_array 0x6a 4 0x70 2 0 0 0x30
           PHP_EACH eff_array AS eff_num => eff_off BEGIN
               PATCH_IF SHORT_AT eff_off = 999 BEGIN
                  WRITE_ASCIIE eff_off ~%template%~
                  WRITE_SHORT eff_off new_opcode
               /*   WRITE_SHORT eff_off+2 template0
                  WRITE_ASCIIE eff_off+0xc ~%template1%~ (8)
                  WRITE_ASCIIE eff_off + 0x24 ~%template2%~ (8)       */
               END
           END

    END
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               make a clone of an effect in an item
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION ~ITM_clone_effect~
           STR_VAR arguments=""
BEGIN
   INNER_ACTION BEGIN
      FAIL ~The ITM_clone_effect function is obsolete~
   END

   WHILE ~%arguments%~ STRING_COMPARE "" BEGIN
    LPF return_first_pair STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET cloned_opcode=key new_opcode=value arguments=list END
    GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x38
    PHP_EACH ab_array AS ab_num => ab_off BEGIN
         SET found_match=0
         GET_OFFSET_ARRAY2 eff_array ab_off ITM_V10_HEAD_EFFECTS
         PHP_EACH eff_array AS eff_num => eff_off BEGIN
            PATCH_IF SHORT_AT eff_off = cloned_opcode BEGIN
               SET found_match=1
               READ_SHORT eff_off+2 template0
               READ_ASCII eff_off+0xc template1 (0x8)
               READ_ASCII eff_off+0x24 template2 (0x8)
               SET insert_num=eff_num
            END
         END
         PATCH_IF found_match=1 BEGIN
           LPF ADD_ITEM_EFFECT
             INT_VAR opcode=999
                     insert_point=insert_num + 1
                     header=ab_num  + 1
           END
           GET_OFFSET_ARRAY2 eff_array ab_off ITM_V10_HEAD_EFFECTS
             PHP_EACH eff_array AS eff_num => eff_off BEGIN
             PATCH_IF SHORT_AT eff_off = 999 BEGIN
                WRITE_SHORT eff_off new_opcode
                WRITE_SHORT eff_off+2 template0
                WRITE_ASCIIE eff_off+0xc ~%template1%~ (8)
                WRITE_ASCIIE eff_off + 0x24 ~%template2%~ (8)
             END
           END
         END
    END
   END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              add an effect (via a patch)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_add_effect
          STR_VAR arguments=""  // this is a patch
                  file_prefix=""
BEGIN
     LPF add_effect STR_VAR arguments = EVALUATE_BUFFER ~%arguments%~ file_prefix=ITM END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_add_effect - -~ END


DEFINE_PATCH_FUNCTION ITM_add_effect_inline
          STR_VAR arguments=""  // this is a patch
                  file_prefix=""
BEGIN
     LPF add_effect_inline STR_VAR arguments file_prefix=ITM END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_add_effect_inline - -~ END


DEFINE_PATCH_FUNCTION ITM_add_equipped_effect
          STR_VAR arguments=""  // this is a patch
                  file_prefix=""
BEGIN
     LPF add_effect STR_VAR arguments = EVALUATE_BUFFER ~%arguments%~ equipped=yes file_prefix=ITM END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_add_equipped_effect - -~ END


DEFINE_PATCH_FUNCTION ITM_add_equipped_effect_inline
          STR_VAR arguments=""
                  file_prefix=""
BEGIN
     LPF add_effect_inline STR_VAR arguments = EVALUATE_BUFFER ~%arguments%~ equipped=yes file_prefix=ITM END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_add_equipped_effect_inline - -~ END


DEFINE_PATCH_FUNCTION ITM_quick_equipped_effect  // defaults to instant/while equipped, target: self
          STR_VAR arguments=""
                  file_prefix=""
BEGIN
     LPF add_effect_inline STR_VAR arguments = EVALUATE_BUFFER ~target=>1 timing=>2 %arguments%~ equipped=yes file_prefix=ITM END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_quick_equipped_effect - -~ END

DEFINE_PATCH_FUNCTION ITM_spell_immunity
          STR_VAR arguments=""
BEGIN
          PATCH_IF ~%arguments%~ STRING_COMPARE_CASE ~~ BEGIN
             LPF return_first_entry STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET entry=entry arguments=list END
             LPF add_effect_inline STR_VAR arguments=EVALUATE_BUFFER ~target=>1 timing=>2 opcode=>206 resource=>%entry% parameter2=>"-1"~ equipped=yes file_prefix=ITM END
             LPF ITM_spell_immunity STR_VAR arguments END
          END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_spell_immunity - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              take an item with one ability, and transfer all effects of that ability onto a new spell
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION copy_item_to_spell 
         STR_VAR item=""
                 spell=""
BEGIN
         // make a new spell
         LAF make_spell STR_VAR spell=EVALUATE_BUFFER ~%spell%~ END
         // add the first ability of the item, and the effects, to the spell
         COPY_EXISTING ~%item%.itm~ ~override~
           READ_ASCII (LONG_AT 0x64) ability (0x38)
           INNER_PATCH_SAVE ability ~%ability%~ BEGIN
             WRITE_BYTE 0x2 4 // innate slots
             WRITE_SHORT 0x10 1 // min level
             DELETE_BYTES 0x26 0x4
             DELETE_BYTES 0x28 0xc
             READ_SHORT 0x1e eff_num
             READ_SHORT 0x20 eff_ind
             WRITE_SHORT 0x20 0
          //   WRITE_ASCII 0x1e ~~ (0x2)
           END
           SET eff_length=0x30*eff_num
           READ_ASCII ((LONG_AT 0x6a) + 0x30*eff_ind) effects (eff_length)
           INNER_ACTION BEGIN
                COPY_EXISTING ~%spell%.spl~ ~override~
                     INSERT_BYTES (LONG_AT 0x64) 0x28
                     WRITE_ASCIIE (LONG_AT 0x64) ~%ability%~
                     WRITE_SHORT 0x68 1
                     WRITE_LONG 0x6a ((LONG_AT 0x6a) + 0x28)
                     INSERT_BYTES (LONG_AT 0x6a) ~eff_length~
                     WRITE_ASCIIE (LONG_AT 0x6a) ~%effects%~
                BUT_ONLY
           END
         BUT_ONLY
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              set level of all extended effects
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_set_level
         INT_VAR arguments=0
BEGIN
  PATCH_CLEAR_ARRAY patch_data
  PATCH_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
   match=>returns_true
   check=>true
   type=>effect
   power=>~%arguments%~
  END
  LPF patch_conditionally STR_VAR arguments=patch_data file_prefix=ITM END

END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_level - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             append the description
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_append_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=append_string_nospace loc=0x54 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_append_description - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             substitute the description
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_substitute_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=substitute_string loc=0x54 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_substitute_description - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set the description
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_set_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0x54 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_description - -~ END


DEFINE_PATCH_FUNCTION ITM_set_unidentified_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0x50 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_unidentified_description - -~ END


DEFINE_PATCH_FUNCTION ITM_say_description
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0x54 say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_say_description - -~ END

DEFINE_PATCH_FUNCTION ITM_say_unidentified_description
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0x50 say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_say_unidentified_description - -~ END

DEFINE_PATCH_FUNCTION ITM_set_both_descriptions
   STR_VAR arguments=""
BEGIN
   LPF ITM_set_description STR_VAR arguments END
   LPF ITM_set_unidentified_description STR_VAR arguments END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_both_descriptions - -~ END


DEFINE_PATCH_FUNCTION ITM_say_both_descriptions
   STR_VAR arguments=""
BEGIN
   LPF ITM_say_description STR_VAR arguments END
   LPF ITM_say_unidentified_description STR_VAR arguments END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_say_both_descriptions - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set the name
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_set_name
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0xc arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_name - -~ END


DEFINE_PATCH_FUNCTION ITM_set_unidentified_name
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0x8 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_unidentified_name - -~ END

DEFINE_PATCH_FUNCTION ITM_say_name
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0xc say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_say_name - -~ END

DEFINE_PATCH_FUNCTION ITM_say_unidentified_name
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0x8 say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_say_unidentified_name - -~ END


DEFINE_PATCH_FUNCTION ITM_set_both_names
   STR_VAR arguments=""
BEGIN
   LPF ITM_set_name STR_VAR arguments END
   LPF ITM_set_unidentified_name STR_VAR arguments END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_set_both_names - -~ END


DEFINE_PATCH_FUNCTION ITM_say_both_names
   STR_VAR arguments=""
BEGIN
   LPF ITM_say_name STR_VAR arguments END
   LPF ITM_say_unidentified_name STR_VAR arguments END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_say_both_names - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             build
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ITM_build
BEGIN
      ///// basic structure
   DELETE_BYTES 0x0 BUFFER_LENGTH
   INSERT_BYTES 0x0 0x72
   // sig
   WRITE_ASCII 0x0 ~ITM V1  ~
  // name
   WRITE_LONG 0x8 ~-1~
   WRITE_LONG 0xc ~-1~
   // max in stack
   WRITE_SHORT 0x38 1
  // description
   WRITE_LONG 0x50 ~-1~
   WRITE_LONG 0x54 ~-1~
   //offsets
   WRITE_LONG 0x64 0x72
   WRITE_LONG 0x6a 0x72
END

DEFINE_PATCH_FUNCTION ITM_postbuild BEGIN
   // sort out relevant proficiency
   SET prof=0
   SPRINT inv ""
   READ_SHORT 0x1c category
   PATCH_MATCH category WITH
   25 BEGIN // axes
      SPRINT inv AX
   END
   28 BEGIN // handheld weapons
      SPRINT inv H6
   END
   69 BEGIN // bastard swords
     SPRINT inv S1
     SET prof=89
   END
   15 BEGIN // bows
     SPRINT inv BW
     SET prof=104
   END
   44 BEGIN // clubs
     SPRINT inv CL
     SET prof=115
   END
   27 BEGIN // crossbows
     SPRINT inv CB
     SET prof=103
   END
   16 BEGIN // daggers
     SPRINT inv DD
     SET prof=96
   END
   24 BEGIN // darts
     SET prof=106
   END
   23 BEGIN // flails
      SPRINT inv FL
      SET prof=100
   END
   57 BEGIN // greatswords
      SPRINT inv S2
      SET prof=93
   END
   30 BEGIN // halberds
      SPRINT inv HB
      SET prof=99
   END
   21 BEGIN // hammers
      SPRINT inv WH
      SET prof=97
   END
   20 BEGIN // longswords
      SPRINT inv S1
      SET prof=90
   END
   17 BEGIN // maces
      SPRINT inv MC
      SET prof=101
   END
   22 BEGIN // morning stars
      SPRINT inv MS
      SET prof=100
   END
   26 BEGIN // quarterstaffs
      SPRINT inv QS
      SET prof=102
   END
   12 BEGIN // shields
       SPRINT inv D2
   END
   19 BEGIN // shortswords
       SPRINT inv SS
       SET prof=91
   END
   18 BEGIN // slings
       SPRINT inv SL
       SET prof=107
   END
   29 BEGIN // spears
      SPRINT inv SP
      SET prof=98
   END
   DEFAULT
   END
   PATCH_IF (prof>0) BEGIN
       LPF ITM_read_proficiency_code RET value=value END
       PATCH_IF value=0 BEGIN
          LPF ITM_proficiency_code STR_VAR arguments=EVALUATE_BUFFER ~%prof%~ END
       END
   END
   PATCH_IF (~%inv%~ STRING_COMPARE ~~) BEGIN
      LPF ITM_read_inventory RET value=value END
      PATCH_IF ~%value%~ STRING_EQUAL_CASE ~~ BEGIN
         LPF ITM_inventory STR_VAR arguments=EVALUATE_BUFFER ~%inv%~ END
      END
   END

END

/////////////////////////////////////////////////////////////////////////////////////////////
///        logs the name of the item
/////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION ITM_log_me STR_VAR filename="" arguments="" BEGIN
   LPF patch_log_this STR_VAR file=EVALUATE_BUFFER ~%arguments%~ input=EVALUATE_BUFFER ~%filename%~ repeat=no END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~ITM_log_me - -~ END


