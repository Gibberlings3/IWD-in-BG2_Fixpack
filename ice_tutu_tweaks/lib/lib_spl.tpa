//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_spell
    STR_VAR
           spell=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%spell%~
                          file_prefix=~SPL~
                          edits
                          editstring
                          allow_missing
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_spell
    STR_VAR
           spell=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%spell%~
                          file_prefix=~SPL~
                          edits
                          editstring
                          allow_missing
         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching regexp
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_all_spells
    STR_VAR
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION regexp_template
                   STR_VAR       file_prefix=~SPL~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~

         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching make
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_spell
    STR_VAR
           spell=""
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION make_template
                  STR_VAR file_list=EVALUATE_BUFFER ~%spell%~
                          file_prefix=~SPL~
                          edits=EVALUATE_BUFFER ~%edits%~
                          editstring=EVALUATE_BUFFER ~%editstring%~
                          build_before= SPL_build
                          build_after = SPL_postbuild
         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< spell_offsets.2da
TYPE       offset_loc      offset_length       entrynum_loc  entrynum_length   entry_length
ability          0x64            4                   0x68          2                 0x28
effect_global    0x6a            4                   0x70          2                 0x30
>>>>>>>>
OUTER_SPRINT ~offset_readin_lookup_2da~ ~spell_offsets~
OUTER_SPRINT ~offset_readin_file_prefix~ ~SPL~
LAUNCH_ACTION_MACRO read_in_offsets


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_fields BEGIN
   name1_string => ~0x8,LONG~
   name2_string => ~0xc,LONG~
   casting_sound => ~0x10,ASCII~
   flags=>~0x18,LONG~
   spell_type => ~0x1c,SHORT~
   priest_type => ~0x20,SHORT~
   casting_animation => ~0x22,BYTE~
   primary => ~0x25,SHORT~
   school => ~0x25,SHORT~
   secondary => ~0x27,BYTE~
   level => ~0x34,LONG~
   spell_icon => ~0x3a,ASCII~
   description1_string => ~0x50,LONG~
   description2_string => ~0x54,LONG~
   num_abils=> ~0x68,SHORT~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=spell_fields function_prefix=SPL END

ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_ability_fields BEGIN
   ability_type => ~0x0,BYTE~
   ability_icon_loc => ~0x2,SHORT~
   ability_icon => ~0x4,ASCII~
   ability_range => ~0xe,SHORT~
   ability_target => ~0xc,BYTE~
   ability_min_level =>~0x10,SHORT~
   ability_damage_type => ~0x1c,SHORT~
   ability_dicesize => ~0x16,SHORT~
   ability_charges=>~0x22,SHORT~
   ability_unknown_late => ~0x24,SHORT~
   projectile => ~0x26,SHORT~
   effect_number => ~0x1e,SHORT~
   casting_time=>~0x12,SHORT~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR offset_base=~offset_base~ lookup_table=spell_ability_fields function_prefix=SPL END


ACTION_DEFINE_ASSOCIATIVE_ARRAY eff_fields BEGIN
   opcode=>~0,SHORT~
   target=>~0x2,BYTE~
   power=>~0x3,BYTE~
   parameter1=>~0x4,LONG~
   parameter2=>~0x8,LONG~
   timing=>~0xc,BYTE~
   resist_dispel=>~0xd,BYTE~
   dispel=>~0xd,BYTE~
   duration=>~0xe,LONG~
   probability1=>~0x12,BYTE~
   probability2=>~0x13,BYTE~
   resource=>~0x14,ASCII~
   dicenum=> ~0x1c,LONG~
   dicesize=> ~0x20,LONG~
   savebonus=> ~0x24,LONG~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=eff_fields offset_base=~offset_base~ function_prefix=SPL secondary="yes" END

ACTION_DEFINE_ASSOCIATIVE_ARRAY spl_bit_fields BEGIN
      breaks_invisibility=>~0x19,2~
      no_LOS=>~0x19,3~
      outdoors_only=>~0x19,5~
      not_magical=>~0x19,6~
      contingency=>~0x19,7~
      not_in_combat=>~0x1a,0~
      bypass_II=>~0x1b,0~
      unusable_chaotic_priest=>~0x1e,0~
      unusable_evil_priest=>~0x1e,1~
      unusable_good_priest=>~0x1e,2~
      unusable_GEneutral_priest=>~0x1e,3~
      unusable_lawful_priest=>~0x1e,4~
      unusable_LCneutral_priest=>~0x1e,5~
      unusable_abjurer=>~0x1e,6~
      unusable_conjurer=>~0x1e,7~
      unusable_diviner=>~0x1f,0~
      unusable_enchanter=>~0x1f,1~
      unusable_illusionist=>~0x1f,2~
      unusable_invoker=>~0x1f,3~
      unusable_necromancer=>~0x1f,4~
      unusable_transmuter=>~0x1f,5~
      unusable_wild_mage=>~0x1f,6~
END

LAF build_flag_data_field_editors STR_VAR lookup_table=spl_bit_fields function_prefix=SPL  END



ACTION_DEFINE_ASSOCIATIVE_ARRAY eff_bit_fields BEGIN
      save_vs_spell => ~0x24,0~
      save_vs_breath => ~0x24,1~
      save_vs_death => ~0x24,2~
      save_vs_poison => ~0x24,2~
      save_vs_wands => ~0x24,3~
      save_vs_petrification => ~0x24,4~
      save_vs_polymorph => ~0x24,4~
      bypass_mirror_image => ~0x27,0~
END

LAF build_flag_data_field_editors STR_VAR lookup_table=eff_bit_fields offset_base=~offset_base~ function_prefix=SPL  secondary="yes" END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               swap opcodes
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ~swap_opcodes_core~
           INT_VAR
                   offset_base=0
           STR_VAR arguments=""
                   file_prefix=""
BEGIN
           LAUNCH_PATCH_FUNCTION ~process_arrow~ STR_VAR input=EVALUATE_BUFFER ~%arguments%~ RET opcode1=key opcode2=value END
           LAUNCH_PATCH_FUNCTION ~%file_prefix%_read_opcode~ INT_VAR offset_base=~offset_base~ RET oldvalue=value END
           PATCH_IF oldvalue=opcode1 BEGIN
             LAUNCH_PATCH_FUNCTION ~%file_prefix%_opcode~INT_VAR offset_base=~offset_base~ STR_VAR arguments=EVALUATE_BUFFER ~%opcode2%~ END
           END

END

DEFINE_PATCH_FUNCTION ~SPL_swap_opcodes~
          STR_VAR arguments=""
                  file_prefix=""
BEGIN
     LAUNCH_PATCH_FUNCTION walk_effect_tree
          STR_VAR function=swap_opcodes_core
                  arguments=EVALUATE_BUFFER ~%arguments%~
                  file_prefix=EVALUATE_BUFFER ~%file_prefix%~
     END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_swap_opcodes - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               delete opcodes
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION ~SPL_delete_opcodes~
             STR_VAR arguments=""
BEGIN
     PATCH_IF ~%arguments%~ STRING_EQUAL_CASE all BEGIN
        SPRINT arguments ~-1~
     END
     PATCH_IF ~%arguments%~ STRING_COMPARE ~~ BEGIN
        LPF return_first_entry STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET arguments=list opcode_to_delete=entry END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete END
        LPF SPL_delete_opcodes STR_VAR arguments END
     END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_delete_opcodes - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              find a spell's actual name from an abbreviated one (i.e., one without the CLERIC_ or WIZARD_
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION deabbreviate_spellname
             STR_VAR input=""
             RET spellname
BEGIN
    TO_UPPER input
    SPRINT spellname ~%input%~
    PATCH_IF VARIABLE_IS_SET EVALUATE_BUFFER ~%%spellname%%~ BEGIN
    END ELSE
    PATCH_IF VARIABLE_IS_SET EVALUATE_BUFFER ~%percentage%WIZARD_%spellname%%percentage%~ AND VARIABLE_IS_SET EVALUATE_BUFFER ~%percentage%CLERIC_%spellname%%percentage%~ BEGIN
        PATCH_PRINT ~Spell %spellname% is ambiguous between WIZARD_%spellname% and CLERIC_%spellname%; assuming WIZARD~
        SPRINT ~spellname~ ~WIZARD_%spellname%~
    END ELSE
    PATCH_IF VARIABLE_IS_SET EVALUATE_BUFFER ~%percentage%WIZARD_%spellname%%percentage%~ BEGIN
        SPRINT ~spellname~ ~WIZARD_%spellname%~
    END ELSE
    PATCH_IF VARIABLE_IS_SET EVALUATE_BUFFER ~%percentage%CLERIC_%spellname%%percentage%~ BEGIN
        SPRINT ~spellname~ ~CLERIC_%spellname%~
    END ELSE BEGIN  // assume it's a bare name
       //PATCH_PRINT ~Warning: cannot identify spell %spellname%~
    END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              find a spell's code from a possibly-abbreviated name (or return the code if given it)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION get_spellcode STR_VAR input="" RET value
BEGIN
   LPF deabbreviate_spellname STR_VAR input RET spellname=spellname END
   SPRINT value EVALUATE_BUFFER ~%%spellname%%~
   INNER_PATCH_SAVE value ~%value%~ BEGIN
      REPLACE_TEXTUALLY ~%percentage%~ ~~
   END
END

DEFINE_ACTION_FUNCTION get_spellcode STR_VAR input="" RET value
BEGIN
   OUTER_INNER_PATCH ~~ BEGIN
      LPF get_spellcode STR_VAR input RET value=value END
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               make a silently-cast prebuffed spell
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION ~build_prebuff_spell~
      STR_VAR spell_ID=""
      RET newcode fullname
BEGIN
     ACTION_IF !VARIABLE_IS_SET EVALUATE_BUFFER ~%%spell_ID%_PREBUFF%~ BEGIN
       OUTER_INNER_PATCH ~~ BEGIN
         LPF deabbreviate_spellname STR_VAR input = EVALUATE_BUFFER ~%spell_ID%~ RET spell_ID=spellname END
       END
       OUTER_SPRINT fullname ~%spell_ID%~
       OUTER_SPRINT ~spellcode~ EVALUATE_BUFFER ~%%spell_ID%%~
       OUTER_INNER_PATCH_SAVE ~newcode~ ~%spellcode%~ BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPWI~ ~dw#sw~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPPR~ ~dw#sp~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPIN~ ~dw#si~
       END
       ACTION_IF !FILE_EXISTS_IN_GAME ~%newcode%.spl~ BEGIN
         COPY_EXISTING ~%spellcode%.spl~ ~override/%newcode%.spl~
             LPF patch_check_ini STR_VAR ini=Conceal_Prebuff_Spell_Names RET value=value END
             PATCH_IF value=1 BEGIN
                  WRITE_LONG 0x8 ~-1~
             END ELSE BEGIN
               LPF patch_check_ini STR_VAR ini=Run_Silent RET value=value END
               PATCH_IF value=0 BEGIN
                  READ_STRREF 0x8 ~spellname~
                  SPRINT ~addon~ @14300
                  SPRINT ~newname~ ~%spellname%~^ ~%addon%~
                  SAY_EVALUATED 0x8 ~%newname%~
               END
             END
             PATCH_MATCH spell_ID WITH
                   CLERIC_BLADE_BARRIER CLERIC_GLOBE_OF_BLADES CLERIC_AURA_OF_FLAMING_DEATH WIZARD_FIRE_SHIELD_RED WIZARD_FIRE_SHIELD_BLUE
             BEGIN
                  LPF SPL_delete_opcodes STR_VAR arguments=174 END
             END
             DEFAULT
                  LPF SPL_delete_opcodes STR_VAR arguments=~139 174 215 141 50~ END
             END
       // BUT_ONLY - should happen anyway
      END
     END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               make a zero-casting-time spell
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION ~build_instant_spell~
      STR_VAR spell_ID=""
      RET newcode spellcode fullname
BEGIN
       OUTER_INNER_PATCH ~~ BEGIN
         LPF deabbreviate_spellname STR_VAR input = EVALUATE_BUFFER ~%spell_ID%~ RET spell_ID=spellname END
       END
       OUTER_SPRINT fullname ~%spell_ID%~
       OUTER_SPRINT ~spellcode~ EVALUATE_BUFFER ~%%spell_ID%%~
       OUTER_INNER_PATCH_SAVE ~newcode~ ~%spellcode%~ BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPWI~ ~dw#0w~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPPR~ ~dw#0p~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPIN~ ~dw#0i~
       END
       ACTION_IF !FILE_EXISTS_IN_GAME ~%newcode%.spl~ BEGIN
       ACTION_CLEAR_ARRAY patch_data
       ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
          type=>ability
          match=>returns_true
          check=>true
          casting_time=>0
       END
       LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%spellcode%=>%newcode%~ editstring=~patch_conditionally=>patch_data~ END
     END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               make a clone of an effect in a spell
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION ~SPL_clone_effect~
           STR_VAR arguments=""
BEGIN
   INNER_ACTION BEGIN
      FAIL ~The SPL_clone_effect function is obsolete~
   END
   WHILE ~%arguments%~ STRING_COMPARE "" BEGIN
    LPF return_first_pair STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET cloned_opcode=key new_opcode=value arguments=list END
    GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x28
    PHP_EACH ab_array AS ab_num => ab_off BEGIN
         SET found_match=0
         GET_OFFSET_ARRAY2 eff_array ab_off ITM_V10_HEAD_EFFECTS
         PHP_EACH eff_array AS eff_num => eff_off BEGIN
            PATCH_IF SHORT_AT eff_off = cloned_opcode BEGIN
               SET found_match=1
               READ_SHORT eff_off+2 template0
               READ_ASCII eff_off+0xc template1 (0x8)
               READ_ASCII eff_off+0x24 template2 (0x8)
               SET insert_num=eff_num
            END
         END
         PATCH_IF found_match=1 BEGIN
           LPF ADD_SPELL_EFFECT
             INT_VAR opcode=999
                     insert_point=insert_num + 1
                     header=ab_num  + 1
           END
           GET_OFFSET_ARRAY2 eff_array ab_off ITM_V10_HEAD_EFFECTS
             PHP_EACH eff_array AS eff_num => eff_off BEGIN
             PATCH_IF SHORT_AT eff_off = 999 BEGIN
                WRITE_SHORT eff_off new_opcode
                WRITE_SHORT eff_off+2 template0
                WRITE_ASCIIE eff_off+0xc ~%template1%~ (8)
                WRITE_ASCIIE eff_off + 0x24 ~%template2%~ (8)
             END
           END
         END
    END
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               build a basic spell
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_build BEGIN
   ///// basic structure
   DELETE_BYTES 0x0 BUFFER_LENGTH
   INSERT_BYTES 0x0 0x72

   // sig
   WRITE_ASCII 0x0 ~SPL V1  ~
  // name
   WRITE_LONG 0x8 ~-1~
   WRITE_LONG 0xc ~-1~
   // spell type
   WRITE_SHORT 0x1c 2
   // primary
   WRITE_SHORT 0x25 9
   // level
   WRITE_LONG 0x34 1
   // unknown
   WRITE_SHORT 0x38 1
   // description
   WRITE_LONG 0x50 ~-1~
   WRITE_LONG 0x54 ~-1~
   //offsets
   WRITE_LONG 0x64 0x72
   WRITE_LONG 0x6a 0x72
END

DEFINE_PATCH_FUNCTION SPL_postbuild
BEGIN
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              add an effect (via a patch)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_add_effect
          STR_VAR arguments=""  // this is a patch
                  file_prefix=""
BEGIN
     LPF add_effect STR_VAR arguments = EVALUATE_BUFFER ~%arguments%~ file_prefix=SPL END
END

DEFINE_PATCH_FUNCTION SPL_add_effect_inline
          STR_VAR arguments=""  // this is a patch
                  file_prefix=""
BEGIN
     LPF add_effect_inline STR_VAR arguments = EVALUATE_BUFFER ~%arguments%~ file_prefix=SPL END
END

LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_add_effect - -~ END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_add_effect_inline - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              set level of all extended effects
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_set_level 
         INT_VAR arguments=0
BEGIN
  PATCH_CLEAR_ARRAY patch_data
  PATCH_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
   match=>returns_true
   check=>true
   type=>effect
   power=>~%arguments%~
  END
  LPF patch_conditionally STR_VAR arguments=patch_data file_prefix=SPL END

END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_set_level - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             append the description
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_append_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=append_string_nospace loc=0x50 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_append_description - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             substitute the description
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_substitute_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=substitute_string loc=0x50 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_substitute_description - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set the description
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_set_description
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0x50 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_set_description - -~ END


DEFINE_PATCH_FUNCTION SPL_say_description
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0x50 say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_say_description - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set the name
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_set_name
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0x8 arguments=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_set_name - -~ END


DEFINE_PATCH_FUNCTION SPL_say_name
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0x8 say=EVALUATE_BUFFER ~%arguments%~ END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_say_name - -~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             add a basic ability
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_add_basic_ability
   STR_VAR arguments=""
BEGIN
   PATCH_MATCH ~%arguments%~ WITH
   spell SPELL BEGIN
       SET locnum=2
   END
   DEFAULT
       SET locnum=4
   END
   PATCH_CLEAR_ARRAY ability_data
   PATCH_DEFINE_ASSOCIATIVE_ARRAY ability_data BEGIN     // these are based on the +hp BGT spells
             type=>ability
             ability_type=> 1 //type:melee
             ability_icon_loc=>~%locnum%~ //location:innate
             ability_target=>1 // target: living actor
             ability_damage_type=> 1// damage type:piercing
             ability_dicesize=> 6 // dice size: 6
             ability_charges => 1
             projectile =>1
   END
   LPF add_entry STR_VAR arguments=ability_data file_prefix=SPL END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_add_basic_ability - -~ END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             swap the resource old=>new
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION SPL_replace_resource
     STR_VAR offset_base=""
             arguments=""
BEGIN
     PATCH_IF ~%arguments%~ STRING_COMPARE_CASE ~~ BEGIN
      LPF return_first_pair STR_VAR list=EVALUATE_BUFFER ~%arguments%~ RET arguments=list old=key new=value END
      LPF SPL_read_resource STR_VAR offset_base RET value=value END
      PATCH_IF ~%value%~ STRING_EQUAL_CASE ~%old%~ BEGIN
           LPF SPL_resource STR_VAR offset_base arguments=EVALUATE_BUFFER ~%new%~ END
      END
      LPF SPL_replace_resource STR_VAR offset_base arguments END
     END
END
LAF log_this STR_VAR file=auto_function_list.txt input=EVALUATE_BUFFER ~SPL_replace_resource - -~ END

